#    utils - utility library
#    Copyright (c) 2021-2022 on-keyday (https://github.com/on-keyday)
#    Released under the MIT license
#    https://opensource.org/licenses/mit-license.php
# frame - http2 frame definition
#generate code
# tool\binred -i src/include/net/http2/frame.bred -o src/include/net/http2/frame.h -Rp -e none unknown -S wrap::shared_ptr wrap::make_shared --license

package utils::net::http2

import <cstdint>
import <string>
import "error_type.h"
import "../../wrap/lite/smart_ptr.h"

struct Frame , H2Error {
    len int $ 3 ! H2Error::read_len = 0
    type FrameType ! H2Error::read_type
    flag Flag ! H2Error::read_flag = Flag::none
    id std::int32_t ? id >= 0 @ H2Error::internal ! H2Error::read_id
}

struct Priority , H2Error {
    depend std::uint32_t ! H2Error::read_depend = 0 
    weight std::uint8_t ! H2Error::read_weight = 0
}

struct DataFrame - Frame ^ type == FrameType::data @ H2Error::type_mismatch ? id != 0 @ H2Error::protocol , H2Error {
    padding std::uint8_t ^ flag & Flag::padded ! H2Error::read_padding = 0 
    data std::string $ len - padding ! H2Error::read_data
    pad std::string $ padding  ! H2Error::read_padding
}

struct HeaderFrame - Frame ^ type == FrameType::header @ H2Error::type_mismatch , H2Error {
    padding std::uint8_t ^ flag & Flag::padded ! H2Error::read_padding = 0
    priority Priority ^ flag & Flag::priority
    data std::string $ len - padding ! H2Error::read_data
    pad std::string $ padding ! H2Error::read_padding
}

struct PriorityFrame - Frame ^ type == FrameType::priority @ H2Error::type_mismatch ? len == 5 , H2Error {
    priority Priority
}

struct RstStreamFrame - Frame ^ type == FrameType::rst_stream @ H2Error::type_mismatch ? len == 4 @ H2Error::frame_size ? id != 0 @ H2Error::protocol , H2Error {
    code std::uint32_t ! H2Error::read_code
}

struct SettingsFrame - Frame ^ type == FrameType::settings @ H2Error::type_mismatch ? len % 6 == 0 @ H2Error::frame_size ? id == 0 @ H2Error::protocol , H2Error {
    setting std::string $ len ^ (flag & Flag::ack) == 0 ! H2Error::read_data
    dummy Dummy ? (flag&Flag::ack==0)||len==0 @ H2Error::frame_size
}

struct PushPromiseFrame - Frame ^ type == FrameType::push_promise @ H2Error::type_mismatch ? id != 0 @ H2Error::protocol , H2Error {
    padding std::uint8_t ^ flag & Flag::padded = 0
    promise std::int32_t ? promise > 0 ! H2Error::internal
    data std::string $ len - padding
    pad std::string $ padding 
}

struct PingFrame - Frame ^ type == FrameType::ping @ H2Error::type_mismatch ? len == 8 @ H2Error::frame_size , H2Error {
    opeque std::uint64_t ? id == 0 @ H2Error::protocol ! H2Error::read_data
}

struct GoAwayFrame - Frame ^ type == FrameType::goaway @ H2Error::type_mismatch , H2Error {
    processed_id std::int32_t ? processed_id >= 0 ! H2Error::read_processed_id
    code std::uint32_t ! H2Error::read_code
    data std::string $ len - 8 ! H2Error::read_data
}

struct WindowUpdateFrame - Frame ^ type == FrameType::window_update @ H2Error::type_mismatch ? len == 4 @ H2Error::frame_size , H2Error {
    increment std::int32_t ? increment > 0 ! H2Error::read_increment
}

struct Continuation - Frame ^ type == FrameType::continuous @ H2Error::type_mismatch ? id != 0 @ H2Error::protocol , H2Error {
    data std::string $ len ! H2Error::read_data
}
