

common ::= config.import("common.bgn")

config.cpp.namespace = "save"
config.cpp.sys_include ="string"
config.cpp.bytes_type = "std::string"

enum UIState:
    :u8
    start
    confrontation
    save_data_select
    create_save_data
    delete_save_data
    ending
    game_start



format PlayerData:
    name :common.Name
    max_items :u8
    max_actions :u8
    item_len :u8
    items :[item_len]Object
    action_len :u8
    actions :[action_len]Object



format SaveData:
    version :u8
    phase: common.Name
    location :common.Name
    players_len :u8
    players :[players_len]PlayerData
    storage :Object


enum ObjectType:
    :u8
    object_null
    object_true
    object_false
    object_int
    object_string
    object_bytes
    object_array
    object_map



format Object:
    object_type :ObjectType
    if object_type == ObjectType.object_int:
        int_value :common.Varint
    elif object_type == ObjectType.object_string:
        string_value :common.Name
    elif object_type == ObjectType.object_bytes:
        bytes_value :common.Name
    elif object_type == ObjectType.object_array:
        array_len :common.Varint
        array :[array_len.value]Object
    elif object_type == ObjectType.object_map:
        map_len :common.Varint
        map :[map_len.value]Pair

format Pair:
    config.order.after = "Object"
    name :common.Name
    value :Object
